package Adaptation_Legitimate

import Types::* 
import sequence_toolkit::*


function checkImage(image: ImageData, model: nat) : Seq(EstimatedPosition) {
	precondition size(image.pixels) == image.height
		/\ (forall i : nat | i >= 1 /\ i <= size(image.pixels) @ size((image.pixels)[i]) == image.width)
	postcondition forall n : nat | n >= 1 /\ n <= size(result) @ 
		(result[n].topLeft)[1] >= 1 /\ (result[n].topLeft)[1] <= image.width
		/\ (result[n].topLeft)[2] >= 1 /\ (result[n].topLeft)[2] <= image.height
		/\ (result[n].bottomRight)[1] >= 1 /\ (result[n].bottomRight)[1] <= image.width
		/\ (result[n].bottomRight)[2] >= 1 /\ (result[n].bottomRight)[2] <= image.height
		/\ result[n].confidence >= 0 /\ result[n].confidence <= 0
}



stm Adaptation_Legitimate { 
	
	 event verifyPlan
	 event planAccepted
	 event planRejected
	
	uses Adaptation_VerificationInfo_events
	uses Adaptation_VerificationInfo_set_events
	uses Adaptation_RecordedData_events
	uses Adaptation_RecordedData
	uses Adaptation_AnalysisResults_events
	uses Adaptation_AnalysisResults
	uses Adaptation_PlanData_events
	uses Adaptation_PlanData

	
	var planLegitimated : boolean
	
	
	const CONFIDENCE_THRESHOLD : real
	
	var i : nat

	initial i0

	 state Initialise { 
	}

	 state WaitForSignal { 
	}

	state PerformVerification {
		entry get_imagesWithLight; imagesWithLight?imagesWithLight_var;
			get_imagesWithPoorLight; imagesWithPoorLight?imagesWithPoorLight_var;
			get_newModel; newModel?newModel_var
			
		initial i0
		
		state CheckImages {}
		
		final f0
		
		transition initialise {
			from i0
			to CheckImages
			
			// assume plan is legitimated until we find a model that doesn't work
			action i = 1; planLegitimated = true
		}
		
		transition imageToCheck {
			from CheckImages
			to CheckImages
			
			condition i <= size(imagesWithPoorLight_var)
			
			// check if any screws were confidently detected
			action planLegitimated =
					let image == (imagesWithLight_var[imagesWithPoorLight_var[i]])[1],
						newPositions == checkImage(image.image, newModel_var),
						confidenceCheck == {screw : EstimatedPosition | screw.confidence >= CONFIDENCE_THRESHOLD} @
					if filter(newPositions, confidenceCheck) == <> then false else planLegitimated end
		}
		
		transition allImagesChecked {
			from CheckImages
			to f0
			
			condition i > size(imagesWithPoorLight_var)
		}
	}

	transition t0 {
		from i0
		to Initialise
	}

	transition t1 {
		from Initialise
		to WaitForSignal
	}

	transition t2 {
		from WaitForSignal
		to PerformVerification
		trigger verifyPlan 
	}

	transition t3 {
		from PerformVerification
		to WaitForSignal
		condition 	planLegitimated  
	
	  	action planAccepted 
	}

	transition t4 {
		from PerformVerification
		to WaitForSignal
		condition 	not planLegitimated  
	
	  	action planRejected 
	}

	 
} 



